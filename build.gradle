// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-alpha5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }

    // Setup the project build directories reletive to a base build
    // directory if one is defined in, for example, a Gradle
    // properties file.
    if (baseBuildDir != null) {
        buildDir = baseBuildDir + (parent == null ? "/${project.name}/build" : "/${rootProject.name}/${project.name}/build")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Define all versions here to facilitate quick and easy version upgrades.
ext {
    // Sdk and tools.  Support Ice Cream Sandwich and up, which should
    // catch ~95% of devices in the wild at first release.  ICS is the
    // minimum SDK to support Facebook signin.
    minSdkVersion = 15
    targetSdkVersion = 23
    compileSdkVersion = 23
    buildToolsVersion = '23.0.2'

    // App dependencies
    appIndexingVersion = '8.4.0'
    espressoVersion = '2.2.2'
    glideVersion = '3.6.1'
    junitVersion = '4.12'
    rulesVersion = '0.5'
    runnerVersion = '0.5'
    supportLibraryVersion = '23.1.1'
    uiautomatorVersion = '2.1.2'

    // Provide environment variables containing secrets for a Google Cloud
    // Platform instance of GameChat using Google Identity Tookit (GIT).
    gitScheme = project.hasProperty('scheme') ? project.getProperty('scheme') : ""
    gitApiKey = project.hasProperty('api_key') ? project.getProperty('api_key') : ""
    gitServerClientId = project.hasProperty('server_client_id') ? project.getProperty('server_client_id') : ""
    gitServerWidgetUrl = project.hasProperty('server_widget_url') ? project.getProperty('server_widget_url') : ""
}
