// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }

    // Configure the project build directories to be relative to a
    // base build directory if one is defined in, for example, a
    // Gradle properties file.  This provides the ability to store the
    // source code on a synced store, such as Google Drive, without
    // incurring the stress, strain and inherent problem in trying to
    // sync build files.  The build files would likely live isolated
    // to each local development system.
    //
    // TODO: A better solution, possibly optimal, would be to provide
    // a Gradle plugin to handle a more complete implementation
    // mirroring the app structure.
    if (rootProject.hasProperty('baseBuildDir') && baseBuildDir != null) {
        buildDir = baseBuildDir + (parent == null ? "/${project.name}/build"
                                   : "/${rootProject.name}/${project.name}/build")
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Define all versions here to facilitate quick and easy version upgrades.
ext {
    // Sdk and tools.  Support Ice Cream Sandwich and up, which should
    // catch ~95% of devices in the wild at first release.  ICS is the
    // minimum SDK to support Facebook signin.
    minSdkVersion = 16
    targetSdkVersion = 23
    compileSdkVersion = 23
    buildToolsVersion = '23.0.3'

    // App dependencies
    espressoVersion = '2.2.2'
    facebookSdkVersion = '4.8.1'
    glideVersion = '3.6.1'
    googlePlayServicesVersion = '9.0.0'
    hamcrestVersion = '1.3'
    junitVersion = '4.12'
    lombokVersion = '1.16.8'
    runnerVersion = '0.5'
    supportLibraryVersion = '23.4.0'
    uiautomatorVersion = '2.1.2'

    // Provide environment variables containing secrets for a Google Cloud
    // Platform instance of GameChat using Google Identity Tookit (GIT).
    gitScheme = project.hasProperty('scheme') ? project.getProperty('scheme') : ""
    gitApiKey = project.hasProperty('api_key') ? project.getProperty('api_key') : ""
    gitServerClientId = project.hasProperty('server_client_id') ? project.getProperty('server_client_id') : ""
    gitServerWidgetUrl = project.hasProperty('server_widget_url') ? project.getProperty('server_widget_url') : ""
}
