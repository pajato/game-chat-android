apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pajato.android.gamechat"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        manifestPlaceholders = [gitScheme:"$rootProject.ext.gitScheme",
                                gitApiKey:"$rootProject.ext.gitApiKey",
                                gitServerClientId:"$rootProject.ext.gitServerClientId",
                                gitServerWidgetUrl:"$rootProject.ext.gitServerWidgetUrl"]
    }

    lintOptions {
        disable 'IconMissingDensityFolder', 'GoogleAppIndexingWarning', 'GradleDependency'
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Run code coverage reports by default on debug builds.
            testCoverageEnabled = true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

dependencies {
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibraryVersion}"
    compile "com.google.android.gms:play-services-appindexing:${googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services:${googlePlayServicesVersion}"
    compile "com.facebook.android:facebook-android-sdk:${facebookSdkVersion}"
    compile(name:'identitytoolkit-api10', ext:'aar')
    compile(name:'identitytoolkit_fbv4', ext:'jar')

    // Dependencies for on device tests.
    androidTestCompile "junit:junit:$rootProject.ext.junitVersion"
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
    androidTestCompile "com.android.support.test.uiautomator:uiautomator-v18:$rootProject.ext.uiautomatorVersion"
}

task printProps << {
    println System.properties
}
